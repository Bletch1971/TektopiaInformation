buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.+'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'

version = project.mod_version
group = project.group_name
archivesBaseName = "${project.mod_id}-${project.minecraft_version}-${project.tektopia_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    version = "${project.minecraft_version}-${project.forge_version}"
    mappings = project.forge_mappings
    makeObfSourceJar = true
    runDir = "run"
}

repositories {  
    maven {
        name = "jei"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
    	name = "ModMaven (jei fallback)"
    	url = "https://modmaven.dev"
    }
    
    maven {
        name = "CurseForge"
        url = "https://www.cursemaven.com"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_1.12.2:${project.jei_version}"

	deobfCompile("curse.maven:hwyla-253449:${project.hwyla_version_id}")
	deobfCompile("curse.maven:the-one-probe-245211:${project.top_version_id}")

	deobfCompile("curse.maven:craftstudio-api-268704:${project.craftstudio_version_id}")
	deobfCompile("curse.maven:tektopia-333029:${project.tektopia_version_id}")
}

//Creates a compiled de-obfuscated jar
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.build.dependsOn('deobfJar')

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                        
        // replace version and mcversion
        expand ([
        	'mod_version': project.mod_version, 
        	'tektopia_version': project.tektopia_version,
        	'jei_version': project.jei_version,
        	'hwyla_version': project.hwyla_version,
        	'top_version': project.top_version,
        	'forge_version': project.forge_version,
        	'minecraft_version': project.minecraft_version 
        ])
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main {
        java {
            srcDir '../../TektopiaCommon/src/main/java'
        }
    }
}
